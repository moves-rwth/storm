include(CMakeFindDependencyMacro)


@PACKAGE_INIT@
@EXP_OPTIONS@


# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
  set(_IMPORT_PREFIX "")
endif()


set(STORM_RESOURCE_INCLUDE_INSTALL_DIR "${_IMPORT_PREFIX}/@STORM_RESOURCE_INCLUDE_INSTALL_DIR@")
set(STORM_RESOURCE_LIBRARY_INSTALL_DIR "${_IMPORT_PREFIX}/@STORM_RESOURCE_LIBRARY_INSTALL_DIR@")


include("${CMAKE_CURRENT_LIST_DIR}/carlConfig.cmake")
if(NOT TARGET lib_carl)
    message(FATAL_ERROR "Including ${CMAKE_CURRENT_LIST_DIR}/carlConfig.cmake did not define target lib_carl.")
endif()
set(storm_carl_DIR "${CMAKE_CURRENT_LIST_DIR}")
include("${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/sylvan/cmake/sylvan/sylvan-config.cmake")

set(CMAKE_MODULE_PATH_save "${CMAKE_MODULE_PATH}")
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}/find_modules/")

find_dependency(Boost)
find_dependency(Z3 4.7.1 NO_MODULE)
find_dependency(Threads)
find_dependency(GLPK)

add_library(glpk SHARED IMPORTED)
set_target_properties(
        glpk
        PROPERTIES
        IMPORTED_LOCATION ${GLPK_LIBRARIES}
        INTERFACE_INCLUDE_DIRECTORIES ${GLPK_INCLUDE_DIR}
)

if(${STORM_HAVE_XERCES})
    find_dependency(XercesC)
endif()
if(${STORM_HAVE_SOPLEX})
    find_dependency(soplex)
endif()


set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH_save}")
unset(CMAKE_MODULE_PATH_save)

add_library(cudd3 STATIC IMPORTED)
set_target_properties(
		cudd3
		PROPERTIES
		IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libcudd@STATIC_EXT@
		INTERFACE_INCLUDE_DIRECTORIES ${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/cudd/
)


if(@STORM_SHIPPED_SPOT@)
    add_library(Storm::spot-bddx STATIC IMPORTED)
        set_target_properties(Storm::spot-bddx PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/spot/"
                IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libbddx.0@DYNAMIC_EXT@
                )

    add_library(Storm::spot STATIC IMPORTED)
    set_target_properties(Storm::spot PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES "${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/spot/"
            IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libspot.0@DYNAMIC_EXT@
            INTERFACE_LINK_LIBRARIES Storm::spot-bddx
    )
else()
    find_dependency(spot)
endif()

# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET storm)
  include("${CMAKE_CURRENT_LIST_DIR}/stormTargets.cmake")
endif()

