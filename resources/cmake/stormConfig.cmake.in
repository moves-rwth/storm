include(CMakeFindDependencyMacro)


@PACKAGE_INIT@
@EXP_OPTIONS@


# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
  set(_IMPORT_PREFIX "")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/_deps/carl-build/carlConfig.cmake")
if(NOT TARGET lib_carl)
    message(FATAL_ERROR "Including _deps/carl-build/carlConfig.cmake did not define target lib_carl.")
endif()
set(storm_carl_DIR "_deps/carl-build")
include("${CMAKE_CURRENT_LIST_DIR}/_deps/sylvanfetch-build/src/cmake/sylvan-config.cmake")

set(CMAKE_MODULE_PATH_save "${CMAKE_MODULE_PATH}")
# Note that this currently assumes that the sources are not moved away from the build tree.
list(INSERT CMAKE_MODULE_PATH 0 "@PROJECT_SOURCE_DIR@/resources/cmake/find_modules/")

find_dependency(Boost QUIET NO_MODULE)
if(@STORM_HAVE_Z3@)
    if(@STORM_Z3_CONFIG@)
        find_dependency(Z3 4.8.7 NO_MODULE)
    else()
        find_dependency(Z3)
        add_library(z3 SHARED IMPORTED)
        set_target_properties(
                z3
                PROPERTIES
                IMPORTED_LOCATION ${Z3_LIBRARIES}
                INTERFACE_INCLUDE_DIRECTORIES ${Z3_INCLUDE_DIR}
        )
    endif()
endif()
find_dependency(Threads)
if(@STORM_HAVE_GLPK@)
    find_dependency(GLPK)

    add_library(glpk SHARED IMPORTED)
    set_target_properties(
            glpk
            PROPERTIES
            IMPORTED_LOCATION ${GLPK_LIBRARIES}
            INTERFACE_INCLUDE_DIRECTORIES ${GLPK_INCLUDE_DIR}
    )
endif()

if(@STORM_HAVE_XERCES@) # STORM_HAVE_XERCES
    find_dependency(XercesC)
endif()
if(@STORM_HAVE_SOPLEX@) # STORM_HAVE_SOPLEX
    find_dependency(soplex)
endif()
if(@STORM_HAVE_GUROBI@) # STORM_HAVE_GUROBI
    find_dependency(GUROBI)
    add_library(GUROBI UNKNOWN IMPORTED)
    set_target_properties(
            GUROBI
            PROPERTIES
            IMPORTED_LOCATION ${GUROBI_LIBRARY}
            INTERFACE_INCLUDE_DIRECTORIES ${GUROBI_INCLUDE_DIRS}
    )
endif()

add_library(cudd3 STATIC IMPORTED)
set_target_properties(
		cudd3
		PROPERTIES
		IMPORTED_LOCATION ${CMAKE_CURRENT_LIST_DIR}/resources/3rdparty/cudd-3.0.0/lib/libcudd@STATIC_EXT@
		INTERFACE_INCLUDE_DIRECTORIES ${CMAKE_CURRENT_LIST_DIR}/resources/3rdparty/cudd-3.0.0/include/
)

if(@STORM_HAVE_SPOT@) # STORM_HAVE_SPOT?
    if(@STORM_SHIPPED_SPOT@)
        add_library(Storm::Spot-bddx STATIC IMPORTED)
            set_target_properties(Storm::Spot-bddx PROPERTIES
                    INTERFACE_INCLUDE_DIRECTORIES "@STORM_3RDPARTY_BINARY_DIR@/spot/include/"
                    IMPORTED_LOCATION @STORM_3RDPARTY_BINARY_DIR@/spot/lib/libbddx@SPOTINFIX@@DYNAMIC_EXT@
                    )

        add_library(Storm::Spot STATIC IMPORTED)
        set_target_properties(Storm::Spot PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "@STORM_3RDPARTY_BINARY_DIR@/spot/include/"
                IMPORTED_LOCATION @STORM_3RDPARTY_BINARY_DIR@/spot/lib/libspot@SPOTINFIX@@DYNAMIC_EXT@
                INTERFACE_LINK_LIBRARIES Storm::Spot-bddx
        )
    else()
        find_dependency(Spot)
        add_library(Storm::Spot UNKNOWN IMPORTED)
        set_target_properties(
                Storm::Spot
                PROPERTIES
                IMPORTED_LOCATION ${Spot_LIBRARIES}
                INTERFACE_INCLUDE_DIRECTORIES ${Spot_INCLUDE_DIR}
        )

    endif()
endif()


if(@STORM_HAVE_MATHSAT@)
    set(MATHSAT_ROOT "@MATHSAT_ROOT@")
    find_dependency(MATHSAT QUIET)
    add_library(mathsat UNKNOWN IMPORTED)
    set_target_properties(
            mathsat
            PROPERTIES
            IMPORTED_LOCATION ${MATHSAT_LIBRARIES}
            INTERFACE_INCLUDE_DIRECTORIES ${MATHSAT_INCLUDE_DIRS}
    )
endif()


set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH_save}")
unset(CMAKE_MODULE_PATH_save)

# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET storm)
  include("${CMAKE_CURRENT_LIST_DIR}/stormTargets.cmake")
endif()

