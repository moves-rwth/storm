include(CMakeFindDependencyMacro)


@PACKAGE_INIT@
@EXP_OPTIONS@


# Compute the installation prefix relative to this file.
get_filename_component(_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
get_filename_component(_IMPORT_PREFIX "${_IMPORT_PREFIX}" PATH)
if(_IMPORT_PREFIX STREQUAL "/")
  set(_IMPORT_PREFIX "")
endif()

set(STORM_RESOURCE_INCLUDE_INSTALL_DIR "${_IMPORT_PREFIX}/@STORM_RESOURCE_INCLUDE_INSTALL_DIR@")
set(STORM_RESOURCE_LIBRARY_INSTALL_DIR "${_IMPORT_PREFIX}/@STORM_RESOURCE_LIBRARY_INSTALL_DIR@")

include("${CMAKE_CURRENT_LIST_DIR}/carlConfig.cmake")
if(NOT TARGET lib_carl)
    message(FATAL_ERROR "Including ${CMAKE_CURRENT_LIST_DIR}/carlConfig.cmake did not define target lib_carl.")
endif()
set(storm_carl_DIR "${CMAKE_CURRENT_LIST_DIR}")
include("${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/sylvan/cmake/sylvan/sylvan-config.cmake")

set(CMAKE_MODULE_PATH_save "${CMAKE_MODULE_PATH}")
list(INSERT CMAKE_MODULE_PATH 0 "${CMAKE_CURRENT_LIST_DIR}/find_modules/")

find_dependency(Boost QUIET NO_MODULE)
if(@STORM_HAVE_Z3@)  # STORM_HAVE_Z3
    if(@STORM_Z3_CONFIG@)
        find_dependency(Z3 4.8.7 NO_MODULE)
    else()
        find_dependency(Z3)
        add_library(z3 SHARED IMPORTED)
        set_target_properties(
                z3
                PROPERTIES
                IMPORTED_LOCATION ${Z3_LIBRARIES}
                INTERFACE_INCLUDE_DIRECTORIES ${Z3_INCLUDE_DIR}
        )
    endif()
endif()
find_dependency(Threads)
if(@STORM_HAVE_GLPK@)  # STORM_HAVE_GLPK
    find_dependency(GLPK)

    add_library(glpk SHARED IMPORTED)
    set_target_properties(
            glpk
            PROPERTIES
            IMPORTED_LOCATION ${GLPK_LIBRARIES}
            INTERFACE_INCLUDE_DIRECTORIES ${GLPK_INCLUDE_DIR}
    )
endif()

if(@STORM_HAVE_XERCES@) # STORM_HAVE_XERCES
    find_dependency(XercesC)
endif()
if(@STORM_HAVE_SOPLEX@) # STORM_HAVE_SOPLEX
    find_dependency(soplex)
endif()
if(@STORM_HAVE_GUROBI@) # STORM_HAVE_GUROBI
    find_dependency(GUROBI)
    add_library(GUROBI UNKNOWN IMPORTED)
    set_target_properties(
            GUROBI
            PROPERTIES
            IMPORTED_LOCATION ${GUROBI_LIBRARY}
            INTERFACE_INCLUDE_DIRECTORIES ${GUROBI_INCLUDE_DIRS}
    )
endif()

add_library(cudd3 STATIC IMPORTED)
set_target_properties(
		cudd3
		PROPERTIES
		IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libcudd@STATIC_EXT@
		INTERFACE_INCLUDE_DIRECTORIES ${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/cudd/
)

if(@STORM_HAVE_SPOT@) # STORM_HAVE_SPOT
    if(@STORM_SHIPPED_SPOT@) # STORM_SHIPPED_SPOT
        add_library(Storm::Spot-bddx SHARED IMPORTED)
            set_target_properties(Storm::Spot-bddx PROPERTIES
                    INTERFACE_INCLUDE_DIRECTORIES "${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/spot/"
                    IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libbddx@DYNAMIC_EXT@
                    )

        add_library(Storm::Spot SHARED IMPORTED)
        set_target_properties(Storm::Spot PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${STORM_RESOURCE_INCLUDE_INSTALL_DIR}/spot/"
                IMPORTED_LOCATION ${STORM_RESOURCE_LIBRARY_INSTALL_DIR}/libspot@DYNAMIC_EXT@
                INTERFACE_LINK_LIBRARIES Storm::Spot-bddx
        )
    else()
        find_dependency(Spot)
        add_library(Storm::Spot UNKNOWN IMPORTED)
        set_target_properties(
                Storm::Spot
                PROPERTIES
                IMPORTED_LOCATION ${Spot_LIBRARIES}
                INTERFACE_INCLUDE_DIRECTORIES ${Spot_INCLUDE_DIR}
        )

    endif()
endif()


if(@STORM_HAVE_MATHSAT@)  # STORM_HAVE_MATHSAT
    set(MATHSAT_ROOT "@MATHSAT_ROOT@")
    find_dependency(MATHSAT QUIET)
    add_library(mathsat UNKNOWN IMPORTED)
    set_target_properties(
            mathsat
            PROPERTIES
            IMPORTED_LOCATION ${MATHSAT_LIBRARIES}
            INTERFACE_INCLUDE_DIRECTORIES ${MATHSAT_INCLUDE_DIRS}
    )
endif()


set(CMAKE_MODULE_PATH "${CMAKE_MODULE_PATH_save}")
unset(CMAKE_MODULE_PATH_save)

# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET storm)
  include("${CMAKE_CURRENT_LIST_DIR}/stormTargets.cmake")
endif()

